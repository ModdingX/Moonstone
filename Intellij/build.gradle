plugins {
    id 'org.jetbrains.intellij.platform' version '2.2.1'
}

repositories {
    intellijPlatform { defaultRepositories() }
}

dependencies {
    compileOnly project(':Core')
    intellijPlatform {
        create('IC', project.ideaVersion)
        bundledPlugins('com.intellij.java', 'com.intellij.gradle')
    }
}

intellijPlatform {
    pluginConfiguration {
        id = project.pluginId
        name = 'Minecraft Moonstone'
        version = project.version
        ideaVersion {
            sinceBuild = project.sinceBuild
            untilBuild = project.untilBuild
        }
    }
}

jar {
    archiveClassifier = 'intellij'
    from zipTree(project(':Core').jar.archiveFile)
}

tasks.register('configureJar') {
    doLast {
        configurations.runtimeClasspath.each { dep ->
            jar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
jar.dependsOn configureJar
instrumentCode.dependsOn configureJar
