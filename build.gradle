buildscript {
    repositories {
        maven { url = 'https://maven.moddingx.org' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.moddingx:LegacyJavaConverter:1.0.1'
    }
}

plugins {
    id 'java-library'
    id 'scala'
    id 'org.jetbrains.intellij' version '1.7.0'
    id 'maven-publish'
}

apply plugin: 'org.moddingx.lcj'

group 'org.moddingx'

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

configurations {
    cursewrapper
    apiElements.extendsFrom cursewrapper
    cursewrapperResolvable.extendsFrom cursewrapper
}

repositories {
    mavenCentral()
    maven { url 'https://maven.moddingx.org/' }
}

dependencies {
    cursewrapper 'org.moddingx:CurseWrapper:3.1'
}

task convertDeps(type: org.moddingx.ljc.gradle.LjcConfigurationTask) {
    input = configurations.cursewrapper
    languageLevel = 11
}

dependencies {
    api 'org.scala-lang:scala-library:2.13.8'
    implementation convertDeps.output()
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    pluginName.set('Minecraft Moonstone')
    type.set('IC')
    version.set(project.ideaVersion)
    downloadSources.set(true)
    
    plugins.set(['java', 'gradle'])
}

patchPluginXml {
    pluginId.set(project.pluginId)
    version.set(project.version)
    sinceBuild.set(project.sinceBuild)
    untilBuild.set(project.untilBuild)
}

task fatjar(type: Jar) {
    classifier "fatjar"
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    dependsOn convertDeps
    doLast {
        configurations.runtimeClasspath.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact fatjar
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url '../moddingx/maven'
        }
    }
}
